buildscript {
    ext {
        kotlinVersion = "1.3.50"
        atomicfuVersion = "0.13.2"
        kotlinxCoroutinesVersion = "1.3.2"
    }

    dependencies {
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:$atomicfuVersion"
    }
}

plugins {
    id "org.jetbrains.kotlin.multiplatform" version "1.3.50" apply false
}

group 'net.halcyxn.nala'
version '0.1.0'

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.multiplatform'
    apply plugin: 'kotlinx-atomicfu'

    kotlin {
        targets.all {
            compilations.all {
                kotlinOptions {
                    freeCompilerArgs += [
                            '-Xuse-experimental=kotlin.Experimental'
                    ]
                }
            }
        }
    }

    tasks.withType(AbstractTestTask) {
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
        }
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "-" * 80
                println "Test results: ${result.resultType} " +
                        "(" +
                        "${result.testCount} tests, " +
                        "${result.successfulTestCount} successful, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped" +
                        ")"
            }
        }
    }
}
